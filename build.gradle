plugins {
    // Required for NeoGradle
//    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
}

subprojects {
    apply plugin: 'java'

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()
    java.withJavadocJar()
        
    jar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
        manifest {
            attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'               : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Built-On-Minecraft'      : minecraft_version
            ])
        }
    }

    sourcesJar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
    }

    repositories {
        mavenCentral()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
        }
    }

    tasks.withType(JavaCompile).configureEach {
    
        it.options.encoding = 'UTF-8'
        it.options.getRelease().set(17)
    }

    processResources {
        def expandProps = [
                "project_group": project.group,
                "version": version,
                "mod_group": mod_group,
                "mod_id": mod_id,
                "mod_environment": mod_environment,
                "mod_name": mod_name,
                "mod_description": mod_description,
                "mod_author": mod_author,
                "mod_credits": mod_credits,
                "mod_license": mod_license,
                "java_version": java_version,
                "minecraft_version": minecraft_version,
                "minecraft_version_range_fabric": minecraft_version_range_fabric,
                "minecraft_version_range_forge": minecraft_version_range_forge,
                "fabric_loader_version": fabric_loader_version,
                "fabric_loader_version_range": fabric_loader_version_range,
                "fabric_api_version": fabric_api_version,
                "forge_loader_version_range": forge_loader_version_range,
                "forge_version": forge_version,
                "forge_version_range": forge_version_range,
//                "neoforge_loader_version_range": neoforge_loader_version_range,
//                "neoforge_version": neoforge_version,
//                "neoforge_version_range": neoforge_version_range,
                "modmenu_version": modmenu_version,
                "homepage_url": homepage_url,
                "sources_url": sources_url,
                "issues_url": issues_url,
                "contact_url": contact_url
        ]

        filesMatching(['pack.mcmeta', 'fabric.mod.json', 'quilt.mod.json', 'META-INF/mods.toml', '*.mixins.json']) {
            expand expandProps
        }
        inputs.properties(expandProps)
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }
}
